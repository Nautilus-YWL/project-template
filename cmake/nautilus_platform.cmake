include_guard()

if(CMAKE_SYSTEM_PROCESSOR STREQUAL "")
  set(CMAKE_SYSTEM_PROCESSOR ${CMAKE_HOST_SYSTEM_PROCESSOR})
endif()
string(TOLOWER "${CMAKE_SYSTEM_PROCESSOR}" NAUTILUS_ARCH_ID)

if(NAUTILUS_ARCH_ID MATCHES "arm.*")
  set(NAUTILUS_ARCH_IS_ARM ON CACHE BOOL "Architecture is arm." FORCE)
elseif(NAUTILUS_ARCH_ID MATCHES "ppc.*" OR NAUTILUS_ARCH_ID MATCHES "powerpc.*")
  set(NAUTILUS_ARCH_IS_POWERPC ON CACHE BOOL "Architecture is PowerPC." FORCE)
elseif(NAUTILUS_ARCH_ID MATCHES "riscv.*")
  set(NAUTILUS_ARCH_IS_RISCV ON CACHE BOOL "Architecture is RISC-V." FORCE)
elseif(NAUTILUS_ARCH_ID MATCHES "x86.*")
  set(NAUTILUS_ARCH_IS_X86 ON CACHE BOOL "Architecture is x86." FORCE)
endif()

if(CMAKE_VERSION VERSION_LESS 3.20)
  include(TestBigEndian)
  test_big_endian(NAUTILUS_IS_BIG_ENDIAN)
  set(NAUTILUS_IS_BIG_ENDIAN ${NAUTILUS_IS_BIG_ENDIAN} CACHE BOOL "Architecture endianness." FORCE)
else()
  foreach(lang C CXX CUDA)
    if(NOT ${CMAKE_${lang}_BYTE_ORDER} STREQUAL "")
      set(NAUTILUS_ARCH_ENDIANNESS ${CMAKE_${lang}_BYTE_ORDER})
    endif()
  endforeach()
  if(NAUTILUS_ARCH_ENDIANNESS STREQUAL "BIG_ENDIAN")
    set(NAUTILUS_IS_BIG_ENDIAN ON CACHE BOOL "Architecture is big endianness." FORCE)
  else()
    set(NAUTILUS_IS_BIG_ENDIAN OFF CACHE BOOL "Architecture is little endianness." FORCE)
  endif()
  unset(NAUTILUS_ARCH_ENDIANNESS)
endif()
