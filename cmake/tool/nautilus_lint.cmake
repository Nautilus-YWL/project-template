include_guard()

function(nautilus_enable_clang_tidy TARGET_NAME)
  if(NOT NAUTILUS_ENABLE_LINT)
    return()
  endif()
  if(NOT NAUTILUS_COMPILER_IS_CLANG)
    message(${NAUTILUS_MESSAGE_WARNING} "clang-tidy requested but compiler is not clang.")
    return()
  endif()

  find_program(CLANG_TIDY_EXE clang-tidy)
  if(NOT CLANG_TIDY_EXE)
    message(${NAUTILUS_MESSAGE_WARNING} "clang-tidy requested but executable not found.")
    return()
  endif()

  set(options)
  set(oneValueArgs)
  set(multiValueArgs OPTIONS)
  cmake_parse_arguments(NAUTILUS_CLANG_TIDY "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  set(lint_command ${CLANG_TIDY_EXE} --use-color)
  if(NAUTILUS_CLANG_TIDY_OPTIONS)
    list(APPEND lint_command "${NAUTILUS_CLANG_TIDY_OPTIONS}")
  endif()
  if(NAUTILUS_WARNINGS_AS_ERRORS)
    list(APPEND lint_command -warnings-as-errors='*')
  endif()
  set_target_properties(${TARGET_NAME} PROPERTIES C_CLANG_TIDY "${lint_command}")
  set_target_properties(${TARGET_NAME} PROPERTIES CXX_CLANG_TIDY "${lint_command}")
endfunction()

function(nautilus_enable_iwyu TARGET_NAME)
  if(NOT NAUTILUS_ENABLE_LINT)
    return()
  endif()

  find_program(IWYU_EXE include-what-you-use)
  if(NOT IWYU_EXE)
    message(${NAUTILUS_MESSAGE_WARNING} "include-what-you-use requested but executable not found.")
    return()
  endif()

  set(options)
  set(oneValueArgs)
  set(multiValueArgs OPTIONS)
  cmake_parse_arguments(NAUTILUS_IWYU "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  set(lint_command ${IWYU_EXE})
  if(NAUTILUS_CLANG_TIDY_OPTIONS)
    list(APPEND lint_command "${NAUTILUS_IWYU_OPTIONS}")
  endif()
  set_target_properties(${TARGET_NAME} PROPERTIES C_INCLUDE_WHAT_YOU_USE "${lint_command}")
  set_target_properties(${TARGET_NAME} PROPERTIES CXX_INCLUDE_WHAT_YOU_USE "${lint_command}")
endfunction()
